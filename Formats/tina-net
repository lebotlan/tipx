
----- Official -----

.net                    ::= (<trdesc>|<pldesc>|<lbdesc>|<prdesc>|<ntdesc>|<netdesc>)*
netdesc                 ::= 'net' <net>
trdesc                  ::= 'tr' <transition> {":" <label>} {<interval>} {<tinput> -> <toutput>}
pldesc                  ::= 'pl' <place> {":" <label>} {(<marking>)} {<pinput> -> <poutput>}
ntdesc                  ::= 'nt' <note> ('0'|'1') <annotation>
lbdesc                  ::= 'lb' [<place>|<transition>] <label>
prdesc                  ::= 'pr' (<transition>)+ ("<"|">") (<transition>)+
interval                        ::= ('['|']')INT','INT('['|']') | ('['|']')INT','w['
tinput                  ::= <place>{<arc>}
toutput                 ::= <place>{<normal_arc>}
pinput                  ::= <transition>{<normal_arc>}
poutput                 ::= <transition>{arc}
arc                     ::= <normal_arc> | <test_arc> | <inhibitor_arc> |
                            <stopwatch_arc> | <stopwatch-inhibitor_arc>
normal_arc              ::= '*'<weight>
test_arc                ::= '?'<weight>
inhibitor_arc           ::= '?-'<weight>
stopwatch_arc           ::= '!'<weight>
stopwatch-inhibitor_arc ::= '!-'<weight>
weight, marking         ::= INT{'K'|'M'|'G'|'T'|'P'|'E'}
net, place, transition, label, note, annotation ::= ANAME | '{'QNAME'}'
INT                     ::= unsigned integer
ANAME                   ::= alphanumeric name, see Notes below
QNAME                   ::= arbitrary name, see Notes below


----- Our implementation -----

.net                    ::= (<trdesc>|<pldesc>|<ntdesc>|<netdesc>)*

netdesc                 ::= 'net' <net>
trdesc                  ::= 'tr' <transition> {<tinput>* '->' <toutput>*}
pldesc                  ::= 'pl' <place> {(<marking>)}
ntdesc                  ::= 'nt' <note> ('0'|'1') <annotation>

tinput                  ::= <place>{<arc>}
toutput                 ::= <place>{<normal_arc>}
arc                     ::= <normal_arc> | <test_arc> | <inhibitor_arc>
normal_arc              ::= '*'<weight>
test_arc                ::= '?'<weight>
inhibitor_arc           ::= '?-'<weight>
weight, marking         ::= INT{'K'|'M'|'G'}

net, place, transition, label, note, annotation ::= ANAME | '{'QNAME'}'

INT                     ::= unsigned integer
ANAME                   ::= alphanumeric name, see Notes below
QNAME                   ::= arbitrary name, see Notes below

- ANAME : any non empty string of letters, digits, primes ’ and underscores _
- ’{’QNAME’}’ : any chain between braces, and in which characters {, }, and \ are prefixed by \ 